
plugins {
	id 'eclipse-wtp'
	id 'java-library'
	id 'war'
	id 'com.bmuschko.tomcat' version '2.5'
}

repositories {
    jcenter()
	mavenCentral()
}

dependencies {
	implementation 'javax:javaee-web-api:8.0.1'
	implementation 'com.sun.mail:javax.mail:1.6.2'
	implementation 'org.simplejavamail:simple-java-mail:5.1.7'
	implementation 'org.slf4j:slf4j-log4j12:1.7.26'
	testImplementation 'junit:junit:4.12'
	tomcat 'org.apache.tomcat.embed:tomcat-embed-core:8.5.42',
	       'org.apache.tomcat.embed:tomcat-embed-logging-juli:8.5.2',
	       'org.apache.tomcat.embed:tomcat-embed-jasper:8.5.42'
	providedRuntime fileTree(dir: 'WebContent/WEB-INF/lib', include: '*.jar')
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

project.webAppDirName = 'WebContent'
project.sourceSets.main.java.srcDirs = ['src']
project.sourceSets.main.resources.srcDirs = ['src']

eclipse {
	classpath {
		defaultOutputDir = file('bin')
		file.whenMerged {
			entries.each { entry ->
				if (entry.kind == 'src' && entry.hasProperty('output')) {
					entry.output = null
				}
			}
		}
	}
	wtp {
		facet {
			facet name: 'jst.java', version: '1.8'
			facet name: 'jst.web', version: '3.1'
			facet name: 'wst.jsdt.web', version: '1.0'
		}
		component {
            deployName = 'GkeiMail'
            contextPath  = 'GkeiMail'
        }
	}
}

tomcat {
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}

war {
    exclude 'WEB-INF/lib/**'
    exclude 'WEB-INF/classes/**'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

